        org 0
        b MAIN                          ;odlazak u glavni dio programa

        org 0x18                        ;mjesto odvijanja prekidnog potprograma
IRQ     
        stmfd sp!, {r0, r1, r2, r3, lr} ;spremanje konteksta - mora se i lr zbog poziva potprograma
        ldr r0, RTC                     ;pospremanje adrese RTC-a
        ldr r1, STANJE                  ;pamcenje stanja

PuD     cmp r1, #1                      ;provjera je li prvo stanje
        moveq r2, #2                    ;ako je sprema koji je sljedece stanje
        bleq LCDS                       ;napisi STANI na LCD
        beq KRAJ                        ;odi na kraj prelaska stanja

DuT     cmp r1, #2                      ;provjera je li 2. stanje
        moveq r2, #3                    ;ako je spremi koji je sljedece
        bleq CiZ                        ;semafor ide na crveno i zuto
        beq KRAJ                        ;odi na zavrsetak prelaska stanja

TuC     cmp r1, #3                      ;provjera je li 3. stanje
        moveq r2, #4                    ;ako je spremi koji je sljedece
        bleq ZELENO                     ;semafor ide na zeleno
        beq KRAJ                        ;odi na zavrsetak prelaska stanja

CuP     cmp r1, #4                      ;provjera je li 4. stanje
        moveq r2, #5                    ;ako je spremi koje je sljedece
        bleq ZUTO                       ;semafor ide na zuto
        beq KRAJ                        ;odi na zavrsetak prelaska stanja

PuS     cmp r1, #5                      ;provjera je li 5. stanje
        moveq r2, #6                    ;ako je spremi koje je sljedece
        bleq CRVENO                     ;semafor ide na crveno
        beq KRAJ                        ;odi na zavrsteka prelaska stanja

SuP     cmp r1, #6                      ;provjera je li 6. stanje
        moveq r2, #1                    ;ako je spremi koje je sljedece
        bleq LCDH                       ;upisi HODAJ na LCD
        beq KRAJ                        ;odi na zavrsetak prelaska stanja

KRAJ    str r2, STANJE                  ;spremanje sljedeceg stanje
        mov r3, #0                      
        str r3, [r0, #8]                ;ciscenje statusa
        str r3, [r0, #12]               ;postavljanje brojaca na 0
        ldmfd sp!, {r0, r1, r2, r3, lr} ;obnova konteksta
        subs pc, lr, #4                 ;izlazak iz prekidnog potprograma

CRVENO  stmfd sp!, {r0, r1}             ;kontekst
        ldr r0, GPIO2                   ;adresa GPIO2
        mov r1, #0b00100000             ;crveno svjetlo
        str r1, [r0]                    ;spremanje na vrata A GPIO2-a
        ldmfd sp!, {r0, r1}             ;obnova konteksta
        mov pc, lr                      ;povratak iz potprograma

ZUTO    stmfd sp!, {r0, r1}             ;kontekst
        ldr r0, GPIO2                   ;adresa GPIO2
        mov r1, #0b01000000             ;zuto svjetlo
        str r1, [r0]                    ;spremanje na vrata A GPIO2-a
        ldmfd sp!, {r0, r1}             ;obnova konteksta
        mov pc, lr                      ;povratak iz potprograma

ZELENO  stmfd sp!, {r0, r1}             ;kontekst
        ldr r0, GPIO2                   ;adresa GPIO2
        mov r1, #0b10000000             ;zeleno svjetlo
        str r1, [r0]                    ;spremanje na vrata A GPIO2-a
        ldmfd sp!, {r0, r1}             ;povratak konteksta
        mov pc, lr                      ;povratak iz potprograma

CiZ     stmfd sp!, {r0, r1}             ;kontekst
        ldr r0, GPIO2                   ;adresa GPIO2
        mov r1, #0b01100000             ;crveno i zuto svjetlo
        str r1, [r0]                    ;spremanje na vrata A GPIO2-a
        ldmfd sp!, {r0, r1}             ;povratak konteksta
        mov pc, lr                      ;povratak iz potprograma

LCDS    stmfd sp!, {r10, lr}            ;kontekst - mora i lr zbog poziva potprograma LCDWR
        mov r10, #0x0d                  ;ciscenje LCDa
        bl LCDWR                        ;upis slova
        mov r10, #0x53                  ;slovo S
        bl LCDWR                        ;upis slova
        mov r10, #0x54                  ;slovo T
        bl LCDWR                        ;upis slova
        mov r10, #0x41                  ;slovo A
        bl LCDWR                        ;upis slova
        mov r10, #0x4e                  ;slovo N
        bl LCDWR                        ;upis slova
        mov r10, #0x49                  ;slovo I
        bl LCDWR                        ;upis slova
        mov r10, #0xa                   ;iz upisnog registra na LCD
        bl LCDWR                        ;upis naredbe
        ldmfd sp!, {r10, lr}            ;obnova konteksta
        mov pc, lr                      ;povratak iz potprograma

LCDH    stmfd sp!, {r10, lr}            ;kontekst - mora i lr zbog poziva potprograma LCDWR
        mov r10, #0x0d                  ;ciscenje LCDa
        bl LCDWR                        ;upis naredbe
        mov r10, #0x48                  ;slovo H
        bl LCDWR                        ;upis slova
        mov r10, #0x4f                  ;slovo O
        bl LCDWR                        ;upis slova
        mov r10, #0x44                  ;slovo D
        bl LCDWR                        ;upis slova
        mov r10, #0x41                  ;slovo A
        bl LCDWR                        ;upis slova
        mov r10, #0x4a                  ;slovo J
        bl LCDWR                        ;upis slova
        mov r10, #0xa                   ;iz upisnog registra na LCD
        bl LCDWR                        ;upis naredbe
        ldmfd sp!, {r10, lr}            ;obnova konteksta
        mov pc, lr                      ;povratak iz potprograma
         

LCDWR   stmfd sp!, {r10}                ;kontekst

        and r10, r10, #0x7f             ; postavi bit 7 u nulu (za svaki slučaj, jer
                                        ; u R0 je tu mogla biti 1) i pošalji znak
        strb r10, [r3]

        orr r10, r10, #0x80             ;podizanje impulsa
        strb r10, [r3]

        and r10, r10, #0x7f             ;spustanje impulsa
        strb r10, [r3]
        
        ldmfd sp!, {r10}                ;obnova konteksta
        mov pc, lr                      ;povratak iz potprograma

MAIN    msr cpsr, #0b11010010           ;inicijalizacija stoga za IRQ
        mov sp, #0x10000

        msr cpsr, #0b11010011           ;inicijalizacija stoga za SVC
        mov sp, #0xfc00
        
        ldr r0, STANJE                  ;ucitavanje stanja
        sub r0, r0, #6
        ldr r3, GPIO1                   ;ucitavanje adrese GPIO1
        add r3, r3, #4                  ;odlazaka na registar podataka vrata B GPIO1a
        ldr r4, GPIO2                   ;ucitavanje adrese GPIO2

        mov r5, #0b11110000             ;omogucavanje izlaza za vrata A GPIO2-a za semafor
        str r5, [r4, #8]

        bl CRVENO                       ;poziv prvog stanja na semaforu
        bl LCDH                         ;poziv prvog stanja na LCDu

        mov r5, #0x0
        ldr r1, RTC                     ;ucitavanje adrese RTC-a
        ldr r2, KONST                   ;konstanta za 5sek
        str r2, [r1, #0x4]              ;stavljanje konstante u match register
        str r5, [r1, #0xc]              ;osvjezavanje brojila
        str r0, [r1, #0x10]             ;dozvoljavanje prekida

        mrs r0, cpsr                    ;dozvola prekida IRQ
        bic r0, r0, #0x80
        msr cpsr, r0    

petlja  b petlja                        ;vrcenje beskonacne petlje dok ne dode do promjene stanja
        
        ;adrese i potrebne mem lokacije
        org 0x1000
GPIO1   dw 0xffff0f00
GPIO2   dw 0xffff0b00
RTC     dw 0xffff0e00
STANJE  dw -5
KONST   dw 0x5